Import('env')

senv = env.Clone()
senv.Append(
	CPPPATH = ['$OM_CPPPATH']
	,LIBPATH = ['$OM_LIBPATH']
	,LIBS = ['m'] + env['OM_LIBS']
)

lib1 = senv.SharedLibrary('st_tables',[
	'st_tables.c'
	,'solinterp/interpolation.c'
	,'solinterp/symmetries.c'
	,'solinterp/tables.c'])

lib4 = senv.SharedLibrary('st_motab',[
	'st_motab.c'
	,'parse.c'])

penv = senv.Clone()

penv.ParseConfig('$PKGCONFIGPYTHON')

lib2 = penv.SharedLibrary('st_solsticepy',['run_py_func.c'])

genv = senv.Clone()
genv.Append(
	LIBS = ['glpk']
)

lib3 = genv.SharedLibrary('st_linprog',['st_linprog.c','linprog/linprog.c'])

shlibs = [lib1,lib4,lib2,lib3]
pysrcs = ['st_test_ext.py','run_solstice.py']

# this is the location that st_simulate etc will use
env.Install('$INSTALL_OM_EXT',shlibs + pysrcs)

# this is the location that you probably want to use with OMEdit
# TODO examine option of placing these things in ~/.openmodelica/binaries ...?
env.Install('#/SolarTherm/Resources/Library',shlibs + pysrcs)

# vim: ts=4:sw=4:noet:tw=80:syntax=python
