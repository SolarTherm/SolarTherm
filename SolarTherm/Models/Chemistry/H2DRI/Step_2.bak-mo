within SolarTherm.Models.Chemistry.H2DRI;

model Step_2
  extends Icons.ChemEqn;
//    A              B                                          C           D
//(1/3)Fe3O4(s) + (1/3)H2(g) ---DeltaH_rxn (J/molrxn) ---> 1.0FeO(s) + (1/3)H2O at different temperatures.
//H2O is at the most stable state at whatever temperature is defined.
  import SI = Modelica.SIunits;
  import CN = Modelica.Constants;
  import CV = Modelica.SIunits.Conversions;
  
  final SI.MolarInternalEnergy R = CN.R "Molar gas constant (J/molK)";
  
  //Imposed conditions
  SI.Temperature T "Reaction temperature (K)";
  SI.Pressure p "Reaction pressure (Pa)";
  
  //Reference conditions at which molar enthalpy of formation is defined at
  parameter SI.Temperature T_ref = 298.15 "Reference temperature 298.15 K";
  parameter SI.Pressure p_ref = 1.0e5 "Reference pressure 100000 Pa";
  
  //Fe2O3(s)
  //Chemical Aspect
  parameter SI.MolarMass M_A = 159.688e-3 "Molar mass of substance A (kg/mol)"; 
  parameter SI.MolarInternalEnergy Hf_A = -825.5e3 "Molar enthalpy of formation of substance A (J/mol)";
  parameter SI.MolarEntropy Sf_A = 87.33 "Molar entropy of substance A at standard conditions (J/molK)";
  parameter Real n_A = 0.5 "Ratio of moles of substance A to moles of reaction (mol/molrxn)";
  //Thermal Aspect
  SI.SpecificEnthalpy h_refA "Reference specific enthalpy of substance A at 298.15K (J/kg)";
  SI.SpecificEnthalpy h_A "Specofic enthalpy of substance A at temperature T (J/kg)";
  SI.SpecificEntropy s_refA "Reference specific entropy of substance A at 298.15K (J/kgK)";
  SI.SpecificEntropy s_A "Specific entropy of substance A at temperature T (J/kgK)";
  
  //H2(g) properties
  parameter SI.MolarMass M_B = 2.016e-3 "Molar mass of substance B (kg/mol)";
  parameter SI.MolarInternalEnergy Hf_B = 0.0 "Molar enthalpy of formation of substance B (J/mol)";
  parameter SI.MolarEntropy Sf_B = 130.68 "Molar entropy of substance B at standard conditions (J/molK)";
  parameter Real n_B = (1.0/6.0) "Ratio of moles of substance B to moles of reaction (mol/molrxn)";
  //Thermal Aspect
  SI.SpecificEnthalpy h_refB "Reference specific enthalpy of substance B at 298.15K (J/kg)";
  SI.SpecificEnthalpy h_B "Specofic enthalpy of substance B at temperature T (J/kg)";
  SI.SpecificEntropy s_refB "Reference specific entropy of substance A at 298.15K (J/kgK)";
  SI.SpecificEntropy s_B "Specific entropy of substance A at temperature T (J/kgK)";
  
  //Fe3O4(s) properties
  parameter SI.MolarMass M_C = 231.533e-3 "Molar mass of substance C (kg/mol)";
  parameter SI.MolarInternalEnergy Hf_C = -1120.89e3 "Molar enthalpy of formation of substance C (J/mol)";
  parameter SI.MolarEntropy Sf_C = 145.245 "Molar entropy of substance B at standard conditions (J/molK)";
  parameter Real n_C = (1.0/3.0) "Ratio of moles of substance C to moles of reaction (mol/molrxn)";
  //Thermal Aspect
  SI.SpecificEnthalpy h_refC "Reference specific enthalpy of substance C at 298.15K (J/kg)";
  SI.SpecificEnthalpy h_C "Specofic enthalpy of substance C at temperature T (J/kg)";
  SI.SpecificEntropy s_refC "Reference specific entropy of substance A at 298.15K (J/kgK)";
  SI.SpecificEntropy s_C "Specific entropy of substance A at temperature T (J/kgK)";
  
  //H20(l) properties
  parameter SI.MolarMass M_D = 18.015e-3 "Molar mass of substance D (kg/mol)";
  parameter SI.MolarInternalEnergy Hf_D = -285.83e3 "Molar enthalpy of formation of substance D (J/mol)"; //note the SI units are the same as molar internal energy
  //Note -285.83e3 J/mol was used, this is for liquid water, since latent heat is already considered in the thermal part.
  parameter SI.MolarEntropy Sf_D = 69.95 "Molar entropy of substance B at standard conditions (J/molK)";
  parameter Real n_D = (1.0/6.0) "Ratio of moles of substance D to moles of reaction (mol/molrxn)";
  //Thermal Aspect
  SI.SpecificEnthalpy h_refD "Reference specific enthalpy of substance D at 298.15K (J/kg)";
  SI.SpecificEnthalpy h_D "Specofic enthalpy of substance D at temperature T (J/kg)";
  SI.SpecificEntropy s_refD "Reference specific entropy of substance A at 298.15K (J/kgK)";
  SI.SpecificEntropy s_D "Specific entropy of substance A at temperature T (J/kgK)";

  //SI.SpecificEnthalpy h_prodC "Enthalpy to produce 1 kg of C (J/kg)";
  
  SI.MolarInternalEnergy H_chem_reactants "Chemical enthalpy of reactants per reaction (J/molrxn)";
  SI.MolarInternalEnergy H_chem_products "Chemical enthalpy of products per reaction (J/molrxn)";
 
  
  SI.MolarInternalEnergy H_therm_reactants "Thermal enthalpy of reactants per reaction (J/molrxn)";
  SI.MolarInternalEnergy H_therm_products "Thermal enthalpy of products per reaction (J/molrxn)";
  
  SI.MolarEntropy S_chem_reactants "Standard entropy of reactants per reaction (J/Kmolrxn)";
  SI.MolarEntropy S_chem_products "Standard entropy of products per reaction (J/Kmolrxn)";
  
  SI.MolarEntropy S_therm_reactants "Delta Entropy of reactants per reaction (J/Kmolrxn)";
  SI.MolarEntropy S_therm_products "Delta Entropy of products per reaction (J/Kmolrxn)";
  
  SI.MolarInternalEnergy H_rxn "Enthalpy change of reaction at temperature T and pressure p per mole of reaction (J/molrxn)";
  SI.MolarInternalEnergy G_rxn "Gibbs free energy of reaction at temperature T and pressure p per mole of reaction (J/molrxn)";
  SI.MolarEntropy S_rxn "Delta Entropy of reaction at temperature T and pressure p per mole of reaction (J/molrxnK)";
  
  Real K_rxn "Equilibrium constant (unspecified units)";
equation

  
  //Specific enthalpy differences (J/kg)
  h_refA = SolarTherm.Media.SolidParticles.Fe2O3_utilities.h_T(T_ref);
  h_A = SolarTherm.Media.SolidParticles.Fe2O3_utilities.h_T(T);
  s_refA = SolarTherm.Media.SolidParticles.Fe2O3_utilities.s_T(T_ref);
  s_A = SolarTherm.Media.SolidParticles.Fe2O3_utilities.s_T(T);
  
  h_refB = Modelica.Media.IdealGases.SingleGases.H2.specificEnthalpy_pT(p_ref,T_ref);  
  h_B = Modelica.Media.IdealGases.SingleGases.H2.specificEnthalpy_pT(p,T);
  s_refB = Modelica.Media.IdealGases.SingleGases.H2.specificEntropy_pTX(p_ref,T_ref,{1.0}); //Mass fraction X is 1.0 for a single gas
  s_B = Modelica.Media.IdealGases.SingleGases.H2.specificEntropy_pTX(p,T,{1.0});
  
  //Modelica.Media.IdealGases.SingleGases.H2.thermalConductivity
  
  h_refC = SolarTherm.Media.SolidParticles.Fe3O4_utilities.h_T(T_ref);
  h_C = SolarTherm.Media.SolidParticles.Fe3O4_utilities.h_T(T);
  s_refC = SolarTherm.Media.SolidParticles.Fe3O4_utilities.s_T(T_ref);
  s_C = SolarTherm.Media.SolidParticles.Fe3O4_utilities.s_T(T);
  
  h_refD = Modelica.Media.Water.IF97_Utilities.h_pT(p_ref,T_ref);
  h_D = Modelica.Media.Water.IF97_Utilities.h_pT(p,T);
  s_refD = Modelica.Media.Water.IF97_Utilities.s_pT(p_ref,T_ref);
  s_D = Modelica.Media.Water.IF97_Utilities.s_pT(p,T);
  
  H_chem_reactants = n_A*Hf_A + n_B*Hf_B; // J/molrxn
  H_chem_products = n_C*Hf_C + n_D*Hf_D; // J/molrxn
  
  H_therm_reactants = n_A*(h_A-h_refA)*M_A + n_B*(h_B-h_refB)*M_B; // J/molrxn
  H_therm_products = n_C*(h_C-h_refC)*M_C + n_D*(h_D-h_refD)*M_D; // J/molrxn
  
  H_rxn = H_chem_products + H_therm_products - H_chem_reactants - H_therm_reactants; // J/molrxn
  //h_prodC = H_rxn/(n_C*M_C);
  
  S_chem_reactants = n_A*Sf_A + n_B*Sf_B;
  S_chem_products = n_C*Sf_C + n_D*Sf_D;
  
  S_therm_reactants = n_A*(s_A-s_refA)*M_A + n_B*(s_B-s_refB)*M_B;// J/Kmolrxn
  S_therm_products = n_C*(s_C-s_refC)*M_C + n_D*(s_D-s_refD)*M_D;// J/Kmolrxn
  
  S_rxn = S_chem_products - S_chem_reactants + S_therm_products - S_therm_reactants;// J/molrxn
  G_rxn = H_rxn - T*(S_rxn);// J/molrxn

  K_rxn = exp(-1.0*G_rxn/(R*T));
annotation(
    Diagram(coordinateSystem(preserveAspectRatio = false)));
end Step_2;