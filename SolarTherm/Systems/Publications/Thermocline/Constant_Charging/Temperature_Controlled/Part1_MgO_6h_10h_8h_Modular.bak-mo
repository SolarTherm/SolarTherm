within SolarTherm.Systems.Publications.Thermocline.Constant_Charging.Temperature_Controlled;

model Part1_MgO_6h_10h_8h_Modular
  //Part one of the documentation studies effect of mesh refinement on output.
  import SI = Modelica.SIunits;
  import CN = Modelica.Constants;
  import CV = Modelica.SIunits.Conversions;
  extends Modelica.Icons.Example;
  package Medium = SolarTherm.Media.Sodium.Sodium_pT;
  //Do not change
  package Fluid_Package = SolarTherm.Materials.Sodium_Table_20K;
  //Do not change
  package Filler_Package = SolarTherm.Materials.MgO_Constant;
  //Design Parameters
  //Fixed
  parameter Integer Correlation = 3 "Conservative";
  parameter SI.Temperature T_min = 510 + 273.15 "Minimum temperature";
  parameter SI.Temperature T_max = 720 + 273.15 "Maximum temperature";
  parameter SI.Temperature T_PB_min = 680 + 273.15 "Minimum tolerated outlet temperature to PB";
  parameter SI.Temperature T_Recv_max = 550 + 273.15 "Maximum tolerated outlet temperature to recv";
  parameter Real eta = 0.26 "Porosity";
  //0.36 if randomly packed, 0.26 for perfect packing.
  //Tanks
  parameter Integer N_f_each = 100 "Number of fluid CVs in main tank";
  //Study this
  parameter Integer N_p = 10 "Number of filler CVs  in main tank";
  //Study this
  parameter SI.Energy E_max = t_discharge * (P_name / eff_PB) "Storage capacity (J), t_discharge(s), 100MWe, 50% PB efficiency";
  parameter Real eff_PB = 0.50 "Power block heat to electricity conversion efficiency";
  parameter SI.Time t_charge = 6.0 * 3600.0 "Charging period";
  parameter SI.Time t_standby = 24.0 * 3600.0 - t_charge - t_discharge "Standby period between discharge and charge";
  parameter SI.Length d_p = 0.10 "Filler diameter";
  //Optimise
  parameter SI.CoefficientOfHeatTransfer U_loss_tank = 0.1 "W/m2K";
  parameter SI.Power P_name = 100.0e6 * (t_charge / t_discharge) "Nameplate power block";
  parameter SI.Time t_discharge = 10.0 * 3600.0 "Discharging period";
  parameter Real ar = 2.0 "Tank aspect ratio";
  //Derived
  parameter SI.Time t_cycle = t_charge + t_discharge + t_standby;
  parameter SI.SpecificEnthalpy h_f_min = Fluid_Package.h_Tf(T_min, 0.0);
  parameter SI.SpecificEnthalpy h_f_max = Fluid_Package.h_Tf(T_max, 1.0);
  parameter SI.MassFlowRate m_charge = E_max / (t_charge * (h_f_max - h_f_min));
  parameter SI.MassFlowRate m_discharge = E_max / (t_discharge * (h_f_max - h_f_min));
  Modelica.Fluid.Sources.Boundary_pT Recv_outlet(redeclare package Medium = Medium, T = T_max, nPorts = 1, p = 101325) annotation(
    Placement(visible = true, transformation(origin = {-112, 48}, extent = {{-16, -16}, {16, 16}}, rotation = 0)));
  Modelica.Fluid.Sources.Boundary_pT PB_outlet(redeclare package Medium = Medium, T = T_min, nPorts = 1, p = 101325) annotation(
    Placement(visible = true, transformation(origin = {92, -60}, extent = {{16, -16}, {-16, 16}}, rotation = 0)));
  //Efficiency
  parameter SI.Energy denom = m_charge * t_charge * (h_f_max - h_f_min);
  SI.Energy numer(start = 0.0);
  Real eff_storage(start = 0.0) "Storage efficiency";
  //COntrol
  SolarTherm.Models.Storage.Thermocline.Modular.Thermocline_Spheres_ModularTank_Top_Final ModuleTank_Top(redeclare package Medium = Medium, redeclare package Fluid_Package = Fluid_Package, redeclare package Filler_Package = Filler_Package, N_f = N_f_each, N_p = N_p, T_max = T_max, T_min = T_min, E_max = E_max/2.0, ar = ar/2.0, eta = eta, d_p = d_p, U_loss_tank = U_loss_tank, Correlation =Correlation) annotation(
    Placement(visible = true, transformation(origin = {-1, 17}, extent = {{-19, -19}, {19, 19}}, rotation = 0)));
    
  SolarTherm.Models.Storage.Thermocline.Modular.Thermocline_Spheres_ModularTank_Bot_Final ModuleTank_Bot(redeclare package Medium = Medium, redeclare package Fluid_Package = Fluid_Package, redeclare package Filler_Package = Filler_Package, N_f = N_f_each, N_p = N_p, T_max = T_max, T_min = T_min, E_max = E_max/2.0, ar = ar/2.0, eta = eta, d_p = d_p, U_loss_tank = U_loss_tank, Correlation = Correlation) annotation(
    Placement(visible = true, transformation(origin = {-1, -17}, extent = {{-19, -19}, {19, 19}}, rotation = 0)));
  
  SolarTherm.Models.Fluid.Sources.FluidSink Recv_Sink(redeclare package Medium = Medium) annotation(
    Placement(visible = true, transformation(origin = {-120, -36}, extent = {{26, -26}, {-26, 26}}, rotation = 0)));
  Modelica.Blocks.Sources.RealExpression Tamb(y = 298.15) annotation(
    Placement(visible = true, transformation(origin = {-40, 2}, extent = {{-12, -18}, {12, 18}}, rotation = 0)));
  Modelica.Blocks.Sources.RealExpression m_flow_Recv(y = m_Recv_signal) annotation(
    Placement(visible = true, transformation(origin = {-103, 5}, extent = {{-19, -17}, {19, 17}}, rotation = 0)));
  SolarTherm.Models.Fluid.Pumps.PumpSimple_EqualPressure pumpSimple_EqualPressure2(redeclare package Medium = Medium) annotation(
    Placement(visible = true, transformation(origin = {-56, -36}, extent = {{10, -10}, {-10, 10}}, rotation = 0)));
  SolarTherm.Models.Fluid.Valves.PBS_TeeJunction thermocline_Splitter1(redeclare package Medium = Medium) annotation(
    Placement(visible = true, transformation(origin = {0, 67.5547}, extent = {{-16, 0}, {16, 22.4453}}, rotation = 0)));
  SolarTherm.Models.Fluid.Valves.PBS_TeeJunction thermocline_Splitter2(redeclare package Medium = Medium) annotation(
    Placement(visible = true, transformation(origin = {-1, -42.1747}, extent = {{-15, 0}, {15, 17.7712}}, rotation = 180)));
  Modelica.Blocks.Sources.RealExpression m_flow_PB(y = m_PB_signal) annotation(
    Placement(visible = true, transformation(origin = {110, 3}, extent = {{20, -19}, {-20, 19}}, rotation = 0)));
  SolarTherm.Models.Fluid.Pumps.PumpSimple pumpSimple_EqualPressure3(redeclare package Medium = Medium) annotation(
    Placement(visible = true, transformation(origin = {40, -60}, extent = {{10, -10}, {-10, 10}}, rotation = 0)));
  SolarTherm.Models.Fluid.Sources.FluidSink PB_Sink(redeclare package Medium = Medium) annotation(
    Placement(visible = true, transformation(origin = {102, 44}, extent = {{-24, -24}, {24, 24}}, rotation = 0)));
  Modelica.Blocks.Sources.RealExpression p_amb(y = 101325) annotation(
    Placement(visible = true, transformation(origin = {57, 2}, extent = {{13, -16}, {-13, 16}}, rotation = 0)));
  SolarTherm.Models.Fluid.Pumps.PumpSimple pumpSimple_EqualPressure(redeclare package Medium = Medium) annotation(
    Placement(visible = true, transformation(origin = {-54, 48}, extent = {{-10, -10}, {10, 10}}, rotation = 0)));
  SolarTherm.Models.Fluid.Pumps.PumpSimple_EqualPressure pumpSimple_EqualPressure1(redeclare package Medium = Medium) annotation(
    Placement(visible = true, transformation(origin = {44, 44}, extent = {{-10, -10}, {10, 10}}, rotation = 0)));
  //Costs
  parameter Real C_fluid = ModuleTank_Bot.C_fluid + ModuleTank_Top.C_fluid;
  parameter Real C_filler = ModuleTank_Bot.C_filler + ModuleTank_Top.C_filler;
  parameter Real C_tank = ModuleTank_Bot.C_tank +ModuleTank_Top.C_tank;
  parameter Real C_insulation = ModuleTank_Bot.C_insulation + ModuleTank_Top.C_insulation;
  parameter Real C_total = ModuleTank_Bot.C_total + ModuleTank_Top.C_total;
  parameter Real C_encapsulation = ModuleTank_Bot.C_encapsulation + ModuleTank_Top.C_encapsulation;
  //Energies
  SI.Energy E_charged(start = 0);
  SI.Energy E_discharged(start = 0);
  SI.Energy E_lost(start = 0);
  SI.Energy E_pump(start = 0);
  SI.MassFlowRate m_Recv_signal(start = m_charge);
  //starts in charging state
  SI.MassFlowRate m_PB_signal(start = 0.0);
  Real T_top_degC;
  Real T_bot_degC;
  Real T_outlet_degC;
  SolarTherm.Models.Fluid.HeatExchangers.mass_loop_breaker mass_loop_breaker annotation(
    Placement(visible = true, transformation(origin = {0, 52}, extent = {{-24, -24}, {24, 24}}, rotation = -90)));
algorithm
  when rem(time, t_cycle) > 1e-6 then
    m_Recv_signal := m_charge;
    m_PB_signal := 0.0;
  end when;
  when rem(time, t_cycle) > t_charge + 1e-6 then
    m_Recv_signal := 0.0;
    m_PB_signal := m_discharge;
  end when;
  when rem(time, t_cycle) > t_charge + t_discharge + 1e-6 then
    m_Recv_signal := 0.0;
    m_PB_signal := 0.0;
  end when;
  when ModuleTank_Bot.T_bot_measured > T_Recv_max then
    m_Recv_signal := 0.0;
  end when;
//when thermocline_Tank.fluid_bot.T > T_Recv_max then
//if rem(time, t_cycle) < t_charge then
//end if;
  when ModuleTank_Top.T_top_measured < T_PB_min then
    m_PB_signal := 0.0;
  end when;
//when thermocline_Tank.fluid_top.T < T_PB_min then
//if rem(time, t_cycle) >= t_charge and rem(time, t_cycle) < t_discharge + t_charge then
//end if;
equation
  T_top_degC = ModuleTank_Top.T_top_measured - 273.15;
  T_bot_degC = ModuleTank_Bot.T_bot_measured - 273.15;
  if ModuleTank_Bot.Tank_A.m_flow > 1e-3 then
//dicharging
    T_outlet_degC = T_top_degC;
  elseif ModuleTank_Bot.Tank_A.m_flow < (-1e-3) then
//charging
    T_outlet_degC = T_bot_degC;
  else
    T_outlet_degC = 25.0;
//reference value
  end if;
/*
//controls
  if rem(time, t_cycle) < t_charge and thermocline_Tank.T_bot_measured < T_Recv_max then
//charging
    m_Recv_signal = m_charge;
    m_PB_signal = 0.0;
  elseif rem(time, t_cycle) >= t_charge and rem(time, t_cycle) < t_discharge + t_charge and thermocline_Tank.T_top_measured > T_PB_min then
//discharging
    m_Recv_signal = 0.0;
    m_PB_signal = m_discharge;
  else
    m_Recv_signal = 0.0;
    m_PB_signal = 0.0;
  end if;
*/
//efficiency
  if time > t_cycle * 5.0 and time < t_cycle * 6.0 then
    der(numer) = PB_Sink.port_a.m_flow * (inStream(PB_Sink.port_a.h_outflow) - h_f_min);
    if time < t_cycle * 5.0 + t_charge then
//charging
      der(E_charged) = ModuleTank_Bot.fluid_a.m_flow * (inStream(ModuleTank_Top.fluid_a.h_outflow) - ModuleTank_Bot.fluid_b.h_outflow);
      der(E_discharged) = 0.0;
    elseif time > t_cycle * 5.0 + t_charge and time < t_cycle * 5.0 + t_charge + t_discharge then
      der(E_charged) = 0.0;
      der(E_discharged) = ModuleTank_Bot.fluid_b.m_flow * (ModuleTank_Top.fluid_a.h_outflow - inStream(ModuleTank_Bot.fluid_b.h_outflow));
    else
      der(E_charged) = 0.0;
      der(E_discharged) = 0.0;
    end if;
    der(E_lost) = ModuleTank_Bot.Tank_A.Q_loss_total + ModuleTank_Top.Tank_A.Q_loss_total;
    der(E_pump) = ModuleTank_Bot.Tank_A.W_loss_pump + ModuleTank_Top.Tank_A.W_loss_pump;
  else
    der(numer) = 0.0;
    der(E_charged) = 0.0;
    der(E_discharged) = 0.0;
    der(E_lost) = 0.0;
    der(E_pump) = 0.0;
  end if;
  if time > t_cycle * 5.0 + 100.0 then
//eff_storage = (numer)/denom;
    eff_storage = (numer - E_pump) / denom;
//subtract pumping power
  else
    eff_storage = 0.0;
  end if;

  connect(thermocline_Splitter2.fluid_b, pumpSimple_EqualPressure2.fluid_a) annotation(
    Line(points = {{-13, -60}, {-34, -60}, {-34, -36}, {-46, -36}}, color = {0, 127, 255}));
  connect(m_flow_Recv.y, pumpSimple_EqualPressure2.m_flow) annotation(
    Line(points = {{-82, 5}, {-56, 5}, {-56, -27}}, color = {0, 0, 127}));
  connect(pumpSimple_EqualPressure3.fluid_b, thermocline_Splitter2.fluid_a) annotation(
    Line(points = {{30, -60}, {11, -60}}, color = {0, 127, 255}));
  connect(pumpSimple_EqualPressure.fluid_b, thermocline_Splitter1.fluid_a) annotation(
    Line(points = {{-44, 48}, {-30, 48}, {-30, 92}, {-13, 92}}, color = {0, 127, 255}));
  connect(thermocline_Splitter1.fluid_b, pumpSimple_EqualPressure1.fluid_a) annotation(
    Line(points = {{13, 92}, {22, 92}, {22, 44}, {34, 44}}, color = {0, 127, 255}));
  
  connect(Recv_outlet.ports[1], pumpSimple_EqualPressure.fluid_a) annotation(
    Line(points = {{-96, 48}, {-64, 48}}, color = {0, 127, 255}));
  connect(PB_outlet.ports[1], pumpSimple_EqualPressure3.fluid_a) annotation(
    Line(points = {{76, -60}, {50, -60}}, color = {0, 127, 255}));
  connect(m_flow_Recv.y, pumpSimple_EqualPressure.m_flow) annotation(
    Line(points = {{-82, 5}, {-74, 5}, {-74, 72}, {-54, 72}, {-54, 56}}, color = {0, 0, 127}));
  connect(m_flow_PB.y, pumpSimple_EqualPressure1.m_flow) annotation(
    Line(points = {{88, 3}, {70, 3}, {70, 70}, {44, 70}, {44, 52}}, color = {0, 0, 127}));
  connect(m_flow_PB.y, pumpSimple_EqualPressure3.m_flow) annotation(
    Line(points = {{88, 3}, {70, 3}, {70, -30}, {40, -30}, {40, -52}}, color = {0, 0, 127}));
  connect(Recv_Sink.port_a, pumpSimple_EqualPressure2.fluid_b) annotation(
    Line(points = {{-94, -36}, {-66, -36}, {-66, -36}, {-66, -36}}, color = {0, 127, 255}));
  connect(PB_Sink.port_a, pumpSimple_EqualPressure1.fluid_b) annotation(
    Line(points = {{78, 44}, {54, 44}, {54, 44}, {54, 44}}, color = {0, 127, 255}));
  connect(thermocline_Splitter1.fluid_c, mass_loop_breaker.port_a) annotation(
    Line(points = {{0, 78}, {0, 66}}, color = {0, 127, 255}));
  connect(ModuleTank_Bot.k_Nf, ModuleTank_Top.k_b) annotation(
    Line(points = {{-7, -5}, {-7, -2}, {-6, -2}, {-6, 5}}, color = {0, 0, 127}));
  connect(ModuleTank_Bot.T_Nf, ModuleTank_Top.T_b) annotation(
    Line(points = {{-5, -5}, {-5, -2}, {-4, -2}, {-4, 5}}, color = {0, 0, 127}));
  connect(ModuleTank_Top.k_1, ModuleTank_Bot.k_a) annotation(
    Line(points = {{3, 5}, {3, -5}}, color = {0, 0, 127}));
  connect(ModuleTank_Bot.T_a, ModuleTank_Top.T_1) annotation(
    Line(points = {{5, -5}, {5, 5}}, color = {0, 0, 127}));
  connect(ModuleTank_Top.fluid_b, ModuleTank_Bot.fluid_a) annotation(
    Line(points = {{-1, 2}, {-1, -2}}, color = {0, 127, 255}));
  connect(ModuleTank_Bot.fluid_b, thermocline_Splitter2.fluid_c) annotation(
    Line(points = {{-2, -32}, {0, -32}, {0, -46}, {0, -46}}, color = {0, 127, 255}));
  connect(mass_loop_breaker.port_b, ModuleTank_Top.fluid_a) annotation(
    Line(points = {{0, 38}, {-1, 38}, {-1, 32}}, color = {0, 127, 255}));
  connect(Tamb.y, ModuleTank_Top.T_amb) annotation(
    Line(points = {{-27, 2}, {-20, 2}, {-20, 16}, {-10, 16}, {-10, 17}}, color = {0, 0, 127}));
  connect(Tamb.y, ModuleTank_Bot.T_amb) annotation(
    Line(points = {{-27, 2}, {-20, 2}, {-20, -18}, {-10, -18}, {-10, -17}}, color = {0, 0, 127}));
  connect(p_amb.y, ModuleTank_Top.p_amb) annotation(
    Line(points = {{42, 2}, {30, 2}, {30, 18}, {8, 18}, {8, 18}}, color = {0, 0, 127}));
  annotation(
    experiment(StopTime = 518400, StartTime = 0, Tolerance = 1e-3, Interval = 300));
end Part1_MgO_6h_10h_8h_Modular;